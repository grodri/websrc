
# 7. Survival Models {#c7s1 .first}

Stata and R have extensive facilities for fitting survival models. In this
course we discuss only the use of Poisson regression to fit piece-wise
exponential survival models. For other approaches see the [survival analysis
course](/survival).

## 7.5 Infant and Child Mortality in Colombia

The datasets section has the original tabulation of children by sex,
cohort, age and survival status (dead or still alive at interview), as
analyzed by Somoza (1980).

As is often the case with survival data, a good part of the effort is to
convert the raw data into the counts of events and exposure needed for
analysis.

### Data Preparation

We will start by reading the data and collapsing over sex, and will then
compute events and exposure to reproduce Table 7.1 in the lecture notes.

```s
	use https://grodri.github.io/datasets/somoza, clear
	collapse (sum) alive (sum) dead, by (cohort age)
```

We make sure the data are sorted by cohort and then age, use `egen` to
count the total number of children in each cohort, and then use
`replace` with a `by cohort` prefix to recompute the number of
children alive at the start of each age group, calculated as the number
who started the previous age group minus those still alive at the
previous age group and minus those who died in the previous age group.
Having done this we can drop kids older than 10, as we are only
interested in survival to age ten.

```s
	sort cohort age // make sure data are sorted
	egen start = total(alive+dead), by(cohort) 
	by cohort: replace start = ///
	  start[_n-1] - alive[_n-1] - dead[_n-1] if _n > 1
	drop if age > 7
```

The next step is to use `recode` to generate a variable representing the
width of the age intervals in months. We then use `generate` to compute
exposure, assuming everyone is exposed the full width of the interval
except those censored or who die in the interval, who are exposed on
average half the interval. We divide by 12 to express exposure in
person-years.

```s
	recode age 4=6 5=12 6=36 7=60, gen(width) // interval width in months
	gen exposure = width * (start - 0.5 * (alive + dead))/12  // in years
```

For convenience we rename dead to `deaths` and set a format, so `exposure`
 prints with one decimal.

We then list the results, which coincide  with Table 7.1 in the notes.

```s
	rename dead deaths
	format expo %8.1f
	list cohort age deaths expo, sep(7)
```

We label the dataset and save it. The resulting file is available in the
datasets section as `somoza2`.

```s
	label data "Infant and Child Mortality in Colombia, 1976"
	notes : "Events and Exposure in Table 7.1, GLM Notes"
	save somoza2, replace
```

### Offset and Predictors

In preparation for modeling, let us calculate the logarithm of exposure
time, to be used as an *offset*.

```s
	gen logexp = log(exposure)
```

### Exponential Survival

Let us fit the null model, which is equivalent to a simple exponential
survival model.
We will also store the estimates for use in later tests.

```s
	poisson deaths, offset(logexp)
	estat gof
	estimates store null
```

Note the astronomical deviance. The estimate of the constant happens to
be the log of the overall mortality rate. Let's verify this fact

```s
	di "Fitted rate = " exp(_b[_cons])
	quietly summarize deaths
	scalar ndeaths = r(sum)
	quietly summarize exposure
	di "Observed Rate = " ndeaths/r(sum)
```

We have an overall mortality rate of 18.4 deaths per thousand
child-years of exposure.

### Three Exponentials

On to the one-factor models. We start with the cohort model, which is
equivalent to a separate exponential survival model for each cohort:

```s
	poisson deaths i.cohort, offset(logexp)
	di exp(_b[2.cohort]), exp(_b[3.cohort]) // 1960-67 amd 1968-76
	estat gof
```

Compare these results with the gross effect estimates in Table 7.3. Note
that the hazard rate declined 26% between the 1941-59 and 1960-67
cohorts, but appears to have increased almost 8% for the 1968-76 cohort
compared to the 1941-59 cohort. (We will return to this issue.)

The extremely large deviance shows that this model does not provide a
reasonable description of the data. It is, however, better than the
model where all cohorts follow the same exponential survival curve, as
evidenced by the model chi-squared or the Wald test.

```s
	lrtest null .
	testparm i.cohort
```

Both tests are highly significant, indicating that overall mortality
rates are not the same across cohorts.

### Piece-Wise Exponential Survival

Now we consider the age model, where the hazard depends on the age of
the child:

```s
	poisson deaths i.age, offset(logexp)
	estimates store age
	mata exp(st_matrix("e(b)"))
	estat gof
```

The age model is equivalent to a piece-wise exponential survival model
with no cohort effects. Compare the results with the gross effects in
Table 7.3. Note the dramatic decrease in risk with age. At age one the
risk of death is only 5% of what it was in the first month of life.

This model still doesn't fit the data, as evidenced by the deviance or
goodness of fit chi-squared. It is, however, a remarkable improvement
over the null, as indicated by the model chi-squared or the Wald test,

```s
	lrtest null .
	testparm i.age
```

You can see why demographers prefer age-specific mortality rates :)

### The Proportional Hazards Model

Now on to the additive model with main effects of age and cohort, which
is equivalent to a proportional hazards model:

```s
	poisson deaths i.age i.cohort, offset(logexp)
	di exp(_b[2.cohort]), exp(_b[3.cohort]) // 1960-67 and 1968-76
	estat gof
```

Note that this model fits reasonably well, with a deviance of 6.18 on 12
d.f., so the assumption of proportional hazards is consistent with the data.

Compare the results with the net effect estimates in Table 7.3, and note
that the anomaly with the youngest cohort has dissappeared. The
estimates now indicate a steady decline in mortality across cohorts.
Taking the 1941-59 cohort as a baseline, mortality at every age from
zero to ten was 28% lower for the 1960-67 cohort and 36% lower for the
more recent 1968-76 cohort. Can you explain why this trend did not
emerge until we controlled for age? *Hint*: the survey was conducted in
1976.

Here's a likelihood ratio test for the cohort effect adjusted for age.
Note that we compare the age model with the additive model that has age
and cohort. That is followed by the Wald test.

```s
	lrtest age .
	testparm i.cohort
```

The cohort differences within age groups are highly significant.

### Estimating Survival Probabilities

Let us calculate the fitted life table shown in Table 7.4 of the lecture
notes.

The `predict` command following a Poisson regression calculates
the expected number of events, so we need to divide by exposure to
obtain fitted rates. An alternative is to use the `xb` and `nooffset`
options (you need both) to obtain the linear predictor or log-hazard,
which you can then exponentiate to obtain the fitted hazard rate.

```s
	predict events
	gen hazard  = events/exposure
```

At this point recall that the age intervals have different widths. We
stored the widths in months in the variable width. 
We will now convert it to years, so it is in the same units as exposure.

```s
	quietly replace width=width/12
```

Now we will sort the data by age within each cohort and calculate the
cumulative hazard for each cohort as a running sum of the hazard times
the interval width. We then use the fact that *S(t)= exp{-Î›(t)}* to
obtain the survival function.

```s
	bysort cohort (age): gen cumhaz = sum(hazard * width)
	gen survival = exp(-cumhaz)
```

The last thing to do is print our handy work.
I will use the `tabulate` command rather than `list` to obtain a suitable 
two-way layout. I specify the "mean" to list the single value in each 
combination of age and cohort.

```s
	tab age cohort, sum(survival) mean
```

We see that the probability of surviving to age one increased from 89.3%
to 92.2% and then to 93.2% across cohorts. The complement of the
probability of surviving to age one is known as the infant mortality
rate (although it is a probability, not a rate) and is usually expressed
per thousand births; it declined from 106.7 to 78.3 to 67.5 across
cohorts.

### Other Methods

For another example of piecewise exponential survival, this time applied
to recidivism and using individual rather than group data, and illustrating
the creating of person-year files, see [this page](recidivism).

Our software is able to fit some of the parametric models discussed
in the bibliographic notes, such as the Weibull model. It also has
non-parametric methods, including procedures for calculating
Kaplan-Meier estimates and for fitting Cox regression models by partial
likelihood. Finally, the data management facilities include facilities
for generating person-year files. For more information see the
[survival course](/survival) pages.

### References

Somoza, J.L. (1980). "Illustrative Analysis: Infant and Child Mortality
in Colombia." *WFS Scientific Reports*, Number 10. Scanned version
[here](https://wfs.dhsprogram.com/WFS-SR/ISI-WFS_SR-10_Samoza_1980_Illustrative%20Analysis%20-%20Infant%20and%20Child%20Mortality%20in%20Colombia.pdf)

<small>Updated fall 2022</small>
