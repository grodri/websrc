
## 3.5. Models with Two Predictors

We now turn to logistic regression models with two or more predictors.
For consistency with the notes we will continue to work with grouped
data. This time we will have 8 groups, defined by the combinations of
four age groups and two categories of desire for more children, as shown
on table 3.7:

```s
	clear
	input ageg nomore users nonusers n
	  1 0  58 265 323
	  1 1  14  60  74
	  2 0  68 215 283
	  2 1  37  84 121
	  3 0  79 230 309
	  3 1 158 145 303
	  4 0  14  43  57
	  4 1  79  58 137
	end
	label define ageg 1 "< 25" 2 "25-29" 3 "30-39" 4 "40-49"
	label values ageg ageg
```

There are five basic models of interest for this dataset. I will fit fit
them all and, when it's all done, reproduce the deviances in Table 3.8.

### The Age Model

Let us start with the age model.

```s
	glm users i.ageg, family(binomial n)
	estimates store ageg
```

Note that the parameter estimates and standard errors are exactly the same
as in Section 3.4, when we worked with only four binomial observations,
showing that grouping the data does not affect any of these statistics.

The model deviance, however, is different. In Section 3.4 the deviance
was zero because the model fitted exactly the four groups. Here we work
with eight groups and get a deviance of 66.48.

### The Additive Model

We are now ready to consider a model with both age and desire for no
more children:

```s
	glm users i.ageg nomore, family(binomial n)
	di exp(_b[nomore])
	estimates store additive
```

Compare the results with Table 3.9 on page 26 of the notes.
Exponentiating the coefficient of "nomore" we get an odds ratio of
2.28. This means that the odds of using contraception among women who
want no more children are double the odds among women in the same age
group who do want more children. The model assumes that the odds ratio
is the same for every age group, an assumption we will need to test.

To test the significance of the odds ratio for no more we can use the
z-statistic given in the output, technically a Wald test. The value
of 7.04 can be squared to obtain a chi-squared statistic on one d.f.
The likelihood ratio test would compare the additive model with the age
model.

```s
	test nomore
	lrtest ageg additive
```

We obtain chi-squared statistics of 49.5 and 49.7 on one d.f., so there
is no doubt that the odds of using contraception in any given age group
vary by whether the women want more children.

### A Model with an Interaction

We now add an interaction between age and desire for no more children.
Obviously we need to use the `i.` prefix for age. If we don't say
anything about `nomore`, but include it in an interaction, Stata will
treat it as a factor. I think the output is a bit cleaner if we remind
Stata that it is just a dummy variable by using the `c.` prefix.

```s
	glm users i.ageg##c.nomore, family(binomial n)
	estimates store nomXage
```

We can test the significance of the interaction
using our old friend `testparm` with just one hash.
[by computing a  Wald test from the coefficients and their variance matrix.]{.r}]
The likekihood ratio test would compare the additive model and the model
with the interaction.

```s
	testparm i.age#c.nomore
	lrtest additive .
```

Can you reproduce the Wald test for all terms involving preferences?

### Reparametrizing Interactions

It may be easier to present the result for this model in terms of odds
ratios by desire for more children in the different age groups, as
discussed on page 27 of the notes, see also Table 3.10.

To this end we need to specify the interaction as usual, but omit the main
effect of wanting no more.

```s
	glm users i.ageg i.ageg#c.nomore, family(binomial n)
	local i ageg#c.nomore 
	di exp(_b[1b.`i']),exp(_b[2.`i']),exp(_b[3.`i']),exp(_b[4.`i'])
```

The parameters now represent differences in the logit scale between
women who do not want more children and those who, do in each of the four
age groups. Exponentiating the coefficients  we find that the odds of u
sing contraception for women who want no more children, compared to those
who do, are 7% higher at age under 25, 39% higher at ages 25-29, three times
as high at age 30-39, and four times as high at age 40-49. The parametrization
used here effectively combines the "main" effect of wanting no more children
(really the effect in the first age group) with the "additional" effect
as one moves to older age groups, in a single number for each age group.
This leads to a more direct presentation of the results.

### The Deviance Table

At this point we have fit all the models listed in Table 3.8 except for
the null model and the model with desire.
We fit these quietly and then print the table.

```s
	quietly glm users       , family(binomial n)
	estimates store null
	quietly glm users nomore, family(binomial n)
	estimates store nomore
	foreach model in null ageg nomore additive nomXage {
	  quietly estimates restore `model'
	  di "`model'" _col(18) %6.2f e(deviance) _col(28) e(df)
	}
```

Make sure you can build tests for the gross and net effects of each factor
from this deviance table.

### Analysis of Covariance Models

We now treat age as a covariate, using the mid-points of the age groups
just as we did before:

```s
	recode ageg 1=20 2=27.5 3=35 4=45, gen(agem)
```

The first model of interest has a linear effect of age, and is analogous
to simple linear regression:

```s
	glm users agem, family(binomial n)
	di exp(_b[agem])
```

The estimated slope shows that the odds of using contraception increase
about six percent per year of age, the same result we obtained in
Section 3.4.

The second model of interest includes additive effects of age and desire
for no more children, and is analogous to an analysis of covariance model:

```s
	glm users agem nomore, family(binomial n)
	di exp(_b[nomore])
```

We see that the odds of using contraception are 128% higher among women
who want no more children than among women who want more and have the
same age. The estimated difference in log-odds when we adjust linearly
for age is very similar to that obtained by treating age as a factor
(0.826 vs. 0.824).

None of these models fits the data very well, so we add an interaction
between age and desire for no more children. As usual, we center age
before constructing the interaction

```s
	gen agec = agem - 30.6
	gen nomXagec = nomore * agec
	glm users agec nomore nomXagec, family(binomial n)
	di exp(_b[agec]), exp(_b[nomore]), exp(_b[nomXagec])
```

The estimates agree with the results in Table 3.12. We see that the odds
of using contraception among women who want more children are about two
percent higher per year of age. The odds among women who want no more
children are double those of women who want more at the mean age (30.6).
This ratio is about five percent higher per year of age above the mean
(and of course five percent lower per year of age below the mean).

Another way to look at this result is to note that the odds of using
contraception among women who want no more children are about 7 percent
higher per year of age. This may be seen more clearly if we parametrize
the model using two constants and two slopes

```s
	gen more = 1-nomore
	gen moreXagec = more*agec
	glm users more moreXagec nomore nomXagec, noconstant family(binomial n)
	di exp(_b[more]),exp(_b[moreXagec]),exp(_b[nomore]),exp(_b[nomXagec])
```

which is the main body of Table 3.12. Testing for equality of slopes is
equivalent to testing the interaction term in the previous specification

```s
	test moreXagec = nomXagec
```

### Plotting Observed and Fitted Logits

Time for a plot. We will reproduce Figure 3.3 in the notes, comparing
the analysis of covariance model we just fitted, a model with a
quadratic effect of age where the curvature is assumed to be the same
for the two groups defined by preferences, and the anova type model
which was saturated for the age by preferences table, and thus represents
observed logits.

```s
	predict lfit, xb
	gen agecsq = agec^2
	quietly glm users agec agecsq nomore nomXagec, family(binomial n)
	predict qfit, xb
	estimates restore nomXage
	predict obs, xb
	local ymore   = obs[7]
	local ynomore = obs[8] + 0.1
	local place placement(e)
	graph twoway (scatter obs agem) ///
	  (line lfit agem if more, lc(blue)) (lin lfit agem if nomore, lc(blue)) ///
	  (mspline qfit agem if more, bands(4) lc(green)) ///
	  (mspline qfit agem if nomore, bands(4) lc(green)) ///
	  ,  title(Figure 3.3: Contraceptive Use by Age and Desire) ///
	     xtitle(age) ytitle(logit) legend(off)  xscale(range(20 50)) ///
		 text(`ymore' 46 "More", `place') text(`ynomore' 46 "No more", `place')
	graph export fig33.png, width(500) replace
```

![](fig33.png){.img-responsive .center-block .stata}

Visually the model with some curvature provides a better fit, but we
have no evidence that it is in fact better than the model with two
straight lines with different slopes.

For purposes of presentation you may consider doing an equivalent plot
in the probability scale. Don't forget that a linear relationship in
the logit scale will be nonlinear in the probability scale, so you
need to rflect the curvature. [The `function` or `mspline` plot types
will be useful in this regard.]{.stata}

<small>Updated fall 2022</small>
