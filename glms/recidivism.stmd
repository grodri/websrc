
## 7.A Piecewise Exponential Models

This is an illustration of piecewise exponential survival models using
individual-level data.
We use Stata, relying on the commands `stset` and `stsplit` to create
pseudo-observations, and `poisson` to fit the model using the Poisson
equivalence. Stata can also fit this model using `streg` with
`distribution(exponential)` on the split data.

### The Data

The dataset we will consider is analyzed in Wooldridge (2002) and
credited to Chung, Schmidt and Witte (1991). The data pertain to a
random sample of convicts released from prison between July 1, 1977 and
June 30, 1978. Of interest is the time until they return to prison. The
information was collected retrospectively by looking at records in April
1984, so the maximum length of observation is 81 months. The data are
available from the Stata website in Stata format.

```s
	use https://www.stata.com/data/jwooldridge/eacsap/recid, clear
	desc, s
```

The file has a censoring indicator, which we subtract from 1 to get a
failure indicator. We also create an id variable and list observation
number 9, which goes back to prison after 54 months.

```s
	gen fail = 1 - cens
	gen id = _n
	list id durat fail in 9    
```

### Creating Pseudo-Observations

To create pseudo-observations for survival analysis using the piecewise
exponential model we `stset` the data ,making sure we specify an id
variable, and then use `stsplit` to split the data into single-year
intervals of duration from 0-12 to 48-60, with an open-ended category
60+. The first command generates the built-in variables `_t0` for
entering time, `_t` for exit time and `_d` for failure. These are
adjusted after the split to reflect what happens in each interval. We
compute exposure as the difference between the exit and entering times.
I also create a variable for the number of events, but this is not
necessary as `_d` would serve the same purpose. We list these variables
for individual 9 after the split to illustrate how the episodes are created.

```s
	stset durat, fail(fail) id(id)
	list id _t0 _t _d if id==9
	stsplit year, at(0 12 24 36 48 60 100)
	gen exposure = _t - _t0
	gen events = _d
	list id _t0 _t _d year exposure events if id==9
```

The sample observation, which goes back to prison after 54 months, contributes
five episodes or pseudo-observations; one each for years one to four, with
12 months of exposure and no events, and another one for year five,  with
6 months of exposure and one event.
Note that the variable generated by Stata to identify episodes, here `year`, 
reflects the time at which the interval *starts*, the same as `_t0`.

### A PWE Proportional Hazards Model

We are now ready to fit a proportional hazards model with a piecewise
exponential baseline where the hazard changes from year to year. We use
the same model as Wooldridge(2002), involving ten predictors, all fixed
covariates.
I specify the offset using the `exposure()` option. I could, of course,
take logs and then use the `offset()` option.]{.stata}

```s
	local predictors workprg priors tserved felon alcohol drugs black married educ age
	poisson events i.year `predictors', exposure(exposure)
	di 1 - exp(_b[felon])
```

We see that the risk of recidivism is about the same in the first two years,
but then decreases substantially with duration since release. At any given
duration, felons have 25% lower risk of recidivism than non-felons with the
same observed characteristics.
Subjects imprisoned for alcohol or drug related offenses have much higher risk
of recidivism, everything else being equal.

### Survival Probabilities

We now illustrate the calculation of survival probabilities, starting
with the baseline hazard.
[There are different ways to do these calculations in Stata, but I
will proceed from first principles using Mata.]{.stata}.
We will retrieve the coefficients, add the constant and the age effects
to obtain the log hazard, exponentiate to obtain hazards, multiply by 12
and sum to obtain the cumulative baseline hazard, and then exponentiate
to obtain the baseline survival. This is not particularly meaningful as
it would apply with all covariates set to zero, including age, which is
measured in months.

```s
	mata
		b = st_matrix("e(b)")
		logh = b[17] :+ b[1..5] // constant is last coefficient
		h = exp(logh)
		H = runningsum(12 * h)
		S = exp(-H)
		S
	end
```

We will now estimate the probability of staying out of prison for five
years given average values of the predictors. First we calculate the mean of
each predictor; we have to be careful to include only one observation per
person, so we restrict the data to the first interval.
The easiest way to compute means in Stata is to `collapse`.

```s
	keep if year == 0
	save recid1, replace
	collapse `predictors'
```

Now that we have the means, we multiply each by the corresponding
coefficient to obtain the linear predictor `xb`, exponentiate to
obtain a relative risk, multiply by the baseline hazard, and then
calculated the predicted survival.

```s
	scalar xb = 0
	foreach var of local predictors {
	  scalar xb = xb + `var' * _b[`var']
	}
	di "xb=" xb
	mata: exp( -H[5] * exp(st_numscalar("xb")) )
```

Thus, the probability of staying out of prison for five years for the average
person is 65.7%.

We now calculate this probability for felons and non-felons, keeping all other
variables at their means. All we need to do is subtract from `xb` the coefficient
of felon times the mean, which gives the linear predictor for a non-felon. We
then add the coefficient of felon to get the linear predictor for a felon. In
both cases the other variables stay at their means.

```s
	scalar xb0 = xb - felon*_b[felon]
	scalar xb1 = xb0 + _b[felon]
	mata exp( -H[5] * exp(st_numscalar("xb0")) )
	mata exp( -H[5] * exp(st_numscalar("xb1")) )
```

The predicted probability is 70.8% for felons and 63.2% for non-felons
when all other characteristics are set to the mean, a difference of 7.6
percentage points. This is a marginal effect at the means.

An alternative calculation sets every person to be a felon or non-felon,
leaving all other characteristics as they are, and then averages the
predicted probability of surviving five years without returning to
prison.
To do this we need the file with the first episode for each person, 
which conveniently I saved. I'll also store the cumulative hazard at duration 
60 in scalar `H5`.

```s
	use recid1, clear
	scalar drop _all // to reuse names xb0 and xb1
	mata st_numscalar("H5", H[5])
	gen xb0 = 0
	local predictors workprg priors tserved felon alcohol drugs black married educ age
	foreach var of local predictors {
	  if "`var'" == "felon" continue
	  quietly replace xb0 = xb0 + `var' * _b[`var']
	}
	gen xb1 = xb0 + _b[felon]
	gen S0 = exp(-H5 * exp(xb0) )
	gen S1 = exp(-H5 * exp(xb1) )
	sum S0 S1 
```

The average probability of staying out of prison for five years is 68.6%
for felons 61.2% for non-felons, a difference of 7.4 percentage points. This
can be interpreted as an average marginal effect.

### References

Wooldridge, Jeffrey M. (2010). *Econometric Analysis of Cross Section and Panel
Data*. 2nd Edition. Cambridge, Massachusetts: The MIT Press.

Chung, C-F, P. Schmidt and A.D. Witte (1991). "Survival Analysis: A Survey".
*Journal of Quantitative Criminology*,__7__:59-98.

<small>Updated fall 2022</small>
